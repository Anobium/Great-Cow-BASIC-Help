==== Millis - Timer based functions

*Introduction*

Intended to provide a simple timing method to applications and libraries.&#160;&#160;
These timer based functions are supported on AVR and PIC.&#160;&#160;
This is achieved by exposing three user interrupt "Hooks" that allow user code to be run as part of the millis() library implementation.&#160;&#160;

These "Hooks" are:

1. `User_Int_sec` - which generates a user interrupt every second

2. `User_Int_ms`  - which generates a user interrupt every millisecond

3. `User_Int_XmS` - which generates a user interrupt every user specified time period expressed in milliseconds.

See each numbered section below shows how to use each type of "hook".

*1. Usage - User_Int_sec*

To define an interrupt every second use: `#Define  User_Int_sec    MyTimerInterrupt` where `User_Int_sec` is mandated.

As follows:
----
    #Define  User_Int_Sec  MyTimerInterrupt
    Init_Millis()
----

Where MyTimerInterrupt, shown above, is called every second and `Init_Millis()` starts the millis library timer.


*2. Usage - User_Int_ms*

To define an interrupt every millisecond use: `#Define  User_Int_ms    MyTimerInterrupt` where `User_Int_ms` is mandated.

As follows:

----
    #Define  User_Int_ms    MyTimerInterrupt
    Init_Millis()

----

Where MyTimerInterrupt, shown above, can be a Subroutine, Function, Macro or Inline Code but the code MUST be short as it is executed within the millis() library interrupt handler.


*Example for PIC:*


A "Blink" example could is written as follows:

----
    #Define  User_Int_mS    Blink(MyLED, 250)
    *Note that  the User_Interrupt can be any name and not only MyTimerInterrupt
    where Blink is:
    Macro Blink(Pin, Period)
      dim  BlinkX as word
      BlinkX += 1
      if BlinkX >= Period then
         BlinkX = 0
         Pin = !Pin
      end if
    end macro
----


PIC “Blink.gcb” example is.

----
    #chip 16f18855
    #option Explicit
    #include "millis.h"

    #Define  User_Int_mS    Blink(MyLED, 250)
    Init_Millis()

    #define MyLED porta.2
    dir MyLED out

    do
      ' main loop is empty but it is required to
      ' prevent the device performing a Sleep call on end
    loop

    end

    Macro Blink(Pin, Period)
      dim  BlinkX as word
      BlinkX += 1
      if BlinkX >= Period then
         BlinkX = 0
         Pin = !Pin
      end if
    end macro
----
The above code was written and tested on the Microchip Xpress Demo Board but, unlike when using the millis() library this is fully portable.

AVR “Blink.gcb” example is.

To change the PIC example to an ARDUINO UNO Board.

Change `#chip 16f18855` to `#chip mega328p,16` and `#define MyLED porta.2` to `#define MyLED portb.5`

The “Blink.gcb” example is.

----
    #chip mega328p,16
    #option Explicit
    #include "millis.h"

    #Define  User_Int_mS    Blink(MyLED, 250)
    Init_Millis()

    #define MyLED portb.5
    dir MyLED out

    do
      ' main loop is empty but it is required to
      ' prevent the device performing a Sleep call on end
    loop

    end

    Macro Blink(Pin, Period)
      dim  BlinkX as word
      BlinkX += 1
      if BlinkX >= Period then
         BlinkX = 0
         Pin = !Pin
      end if
    end macro
----


*3. Usage - User_Int_Xms*

To define an interrupt at a specified period use: `#Define User_Int_XmS   type, period, task` where `User_Int_XmS` is mandated.

Where `type` is Byte, Word, Long or any valid variable, `period` is the number of mS between the call to the hook, `task` is the Macro, Sub or Inline code to be run when the hook is called.

As follows:

----
    #Define  User_Int_XmS    Word, 500, MyLED = !MyLED
    Init_Millis()

----

The  “Blink.gcb” example using User_Int_XmS is.&#160;&#160;This example calls the hook every 500ms to toggle MyLED.
----
        #chip 16f18855
        #option Explicit
        #include "millis.h"

        #define MyLED porta.2
        dir MyLED out

        #Define  User_Int_XmS    Word, 500, MyLED = !MyLED
        Init_Millis()

        do
          ' main loop is empty as everything happens within
          ' the user hook. It is required, however, to
          ' prevent the device going to Sleep.
        loop

        end
----
